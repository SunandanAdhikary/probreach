model: pha;
/*
 * the model is accurate for 0-100 km/h period
 */
#define v_max 55.56 // [200 km/h] maximum speed of a car (m/s)
#define v_100 27.78 // m/s
#define drag 3.028e-4 // some average value (1/m)
#define a_d 5.25
#define mu 1
#define alpha 0.05776 
#define t_react 1.2

[0,1000] s; // distance m
[0,v_100] v; // velocity m/s (16.67 m/s = 60 km/h)
[0,30] tau; // time in seconds
[0,30] time;
dist_normal(1.65, 0.2) a_a;

// accelerating
{
mode 1;
flow:
d/dt[s]= v;
d/dt[v]= a_a;
d/dt[tau]= 1.0;
jump:
(and (v >= v_100))==>@2(and (s' = s) (v' = v) (tau' = 0)(a_a'=a_a));
}

// reacting
{
mode 2;
flow:
d/dt[s]= v;
d/dt[v]= 0;
d/dt[tau]= 1.0;
jump:
(and (tau = t_react))==>@3(and (s' = s) (v' = v) (tau' = 0)(a_a'=a_a));
}

// braking
{
mode 3;
flow:
d/dt[s]= v;
d/dt[v]= - a_d;
d/dt[tau]= 1.0;
jump:
(and (v <= 0))==>@4(and(s'=s)(v'=v)(tau'=0)(a_a'=a_a));
}

// stopped
{
mode 4;
flow:
d/dt[s]= v;
d/dt[v]= 0.0;
d/dt[tau]= 1.0;
jump:

}

init:
@1(and (s = 0) (v = 0) (tau = 0));

goal:
//@4(and(tau=0)(s>=400));
@4(and(s>=400));
//@1(and (v>=v_100));
//@2(and (tau = t_react)(s>=500));
//@3(and(v<=0));